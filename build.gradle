import org.apache.tools.ant.filters.ReplaceTokens

allprojects {
    apply plugin: "java"
}

subprojects {
    repositories {
        maven { url "https://maven.aliyun.com/repository/public" }

        maven { url "https://hub.spigotmc.org/nexus/content/groups/public" }
        maven { url "https://libraries.minecraft.net" }
        mavenCentral()
    }

    dependencies {
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
    }

    processResources {
        String env = System.getProperty("env", "AUTO")
        String ver;
        if (env.toLowerCase() == "final") {
            ver = (project.plugin_version as String)
        } else {
            ver = "Build_" + ((System.currentTimeMillis() / 1000) as int)
        }

        setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)

        def props = new Properties()
        props.put("version", ver)
        props.put("author", 'ksqeib, CaaMoe')
        filter(tokens: props, ReplaceTokens)
    }
}